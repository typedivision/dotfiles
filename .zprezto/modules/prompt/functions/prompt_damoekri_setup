#
# A simple theme inspired by the Sorin and PeepCode themes.
#
# Authors:
#   Daniel MÃ¸ller Kristensen <damoekri@icloud.com>
#
# Screenshots:
#   http://i.imgur.com/AX9HnPF.png
#

# Load dependencies.
pmodload 'helper'

prompt_damoekri_precmd() {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  _prompt_damoekri_pwd=$(prompt-pwd)

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_damoekri_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_damoekri_precmd

  # Tell prezto we can manage this prompt
  zstyle ':prezto:module:prompt' managed 'yes'

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:keymap:primary' format ' %(?.%F{green}.%F{yellow})>%f'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format ':%F{yellow}%s%f'
  zstyle ':prezto:module:git:info:branch' format '%F{magenta}%b%f'
  zstyle ':prezto:module:git:info:clean' format ''
  zstyle ':prezto:module:git:info:dirty' format ' %F{red}x%f'
  zstyle ':prezto:module:git:info:commit' format '%F{magenta}%.7c%f'
  zstyle ':prezto:module:git:info:position' format '%F{magenta}%p%f'
  zstyle ':prezto:module:git:info:keys' format \
    'rprompt' ' $(coalesce "%b" "%p" "%c")%s%C%D'

  # Define prompts.
  PROMPT='%F{cyan}${_prompt_damoekri_pwd}%f${git_info:+${(e)git_info}}${editor_info[keymap]} '
}

prompt_damoekri_setup "$@"
# vim: ft=zsh
